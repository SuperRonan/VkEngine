#version 460

// #define I_WANT_TO_DEBUG 1
// #define DEBUG_ENABLE_DEBUG_GLOBAL_SIGNAL 1
// #include <ShaderLib:/Debug/DebugBuffers.glsl>

#include "common.glsl"

#include "shading.glsl"

layout(SHADER_DESCRIPTOR_BINDING + 0, rgba32f) uniform restrict readonly image2D AlbedoTexture;
layout(SHADER_DESCRIPTOR_BINDING + 1, rgba32f) uniform restrict readonly image2D PositionTexture;
layout(SHADER_DESCRIPTOR_BINDING + 2, rgba32f) uniform restrict readonly image2D NormalTexture;
layout(SHADER_DESCRIPTOR_BINDING + 3, rgba32f) uniform restrict readonly image2D TangentTexture;

layout(SHADER_DESCRIPTOR_BINDING + 4) uniform sampler2D AOTexture;

layout(SHADER_DESCRIPTOR_BINDING + 5, rgba16f) uniform restrict writeonly image2D ColorOutput;

layout(local_size_x = 16, local_size_y = 16) in;

#ifndef USE_AO
#define USE_AO 0
#endif

void main()
{
	const ivec2 pixel = ivec2(gl_GlobalInvocationID.xy);
	const ivec2 dims = imageSize(AlbedoTexture);

	if(all(lessThan(pixel, dims)))
	{
		const vec2 screen_uv = (vec2(pixel) + 0.5) / vec2(dims);

		const bool force_white_albedo = false;
		vec3 albedo;
		if(force_white_albedo)
		{
			albedo = vec3(1);
		}
		else
		{
			albedo = imageLoad(AlbedoTexture, pixel).xyz;
		}


		const vec3 position = imageLoad(PositionTexture, pixel).xyz;
		const vec3 normal = imageLoad(NormalTexture, pixel).xyz;

#if DEBUG_ENABLE_DEBUG_GLOBAL_SIGNAL
		if(pixel == ivec2(100, 100))
		{
			_g_debug_signal = true;
		}
#endif

		vec3 res = shade(albedo, position, normal);

		float ao = 1;
#if USE_AO
		ao = textureLod(AOTexture, screen_uv, 0).x;
#endif
		res += ao * albedo * scene_ubo.ambient;

		imageStore(ColorOutput, pixel, vec4(res, 0));
	}

    
}